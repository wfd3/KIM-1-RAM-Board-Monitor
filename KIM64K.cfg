MEMORY {
	ZP:			start = $0000, size = $00FE, type = rw, file = "";
	RAM:		start = $0200, size = $01FF, type = rw, file = "";
	BASIC:		start = $C000, size = $2300, fill = yes, file = %O;
	XKIM:		start = $E300, size = $1B95, fill = yes, file = %O;
	WOZMON:		start = $FE95, size = $014D, fill = yes, file = %O;
	TRAMP:		start = $FFE2, size = $0018, fill = yes, file = %O;
	VECS:		start = $FFFA, size = $0006, fill = yes, file = %O;
	DUMMY:		start = $0000, size = $00FF, file = "";
}

SEGMENTS {
	ZEROPAGE:	load = ZP,      type = zp;
	XKIMDATA:   load = RAM,     type = rw; # not included in ROM

	# MS Basic
	HEADER:		load = BASIC,	type = ro, define = no;
	VECTORS:	load = BASIC,	type = ro, define = no;
	KEYWORDS:	load = BASIC,	type = ro, define = yes; # We'll use this as a label in MSBasic/kimrom_extras.s
	ERROR:		load = BASIC,	type = ro, define = no;
	CODE:		load = BASIC,	type = ro, define = no;
	CHRGET:		load = BASIC,	type = ro, define = no;
	INIT:		load = BASIC,	type = ro, define = yes; # We'll use this as a lable in xKIM/xKIM.s
	EXTRA:		load = BASIC,	type = ro, define = no;

	# xKIM
	XKIM:		load = XKIM, 	type = ro, define = yes;
	WOZMON:		load = WOZMON,  type = ro, define = yes;
	TRAMP:		load = TRAMP,   type = ro, define = yes;
	VECS:		load = VECS, 	type = ro, define = no;
	
	# Don't include
	DUMMY:		load = DUMMY;
}

